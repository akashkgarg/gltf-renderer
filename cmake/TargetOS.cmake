include_guard(GLOBAL)

# Normalized & consistent CMake variables and preprocessor defines for checking the build and target platforms

if(NOT TARGET_OS)
    if(ANDROID)
        set(TARGET_OS "android")
    elseif(IOS)
        set(TARGET_OS "ios")
    elseif(${CMAKE_SYSTEM_NAME} STREQUAL "Darwin")
        set(TARGET_OS "macos")
    elseif(${CMAKE_SYSTEM_NAME} STREQUAL "Linux")
        set(TARGET_OS "linux")
    elseif(${CMAKE_SYSTEM_NAME} STREQUAL "Windows")
        set(TARGET_OS "windows")
    elseif(${CMAKE_SYSTEM_NAME} STREQUAL "Emscripten")
        set(TARGET_OS "web")
    elseif(${CMAKE_SYSTEM_PROCESSOR} STREQUAL "wasm32")
        set(TARGET_OS "web")
    else()
        message(FATAL_ERROR "Don't know how to deal with CMAKE_SYSTEM_NAME \"${CMAKE_SYSTEM_NAME}\"")
    endif()
endif()

if(${TARGET_OS} STREQUAL "android")
    set(TARGET_OS_ANDROID YES)
    set(TARGET_OS_MOBILE YES)
    set(TARGET_OS_UNIX YES)
elseif(${TARGET_OS} STREQUAL "ios")
    set(TARGET_OS_IOS YES)
    set(TARGET_OS_APPLE YES)
    set(TARGET_OS_MOBILE YES)
    set(TARGET_OS_UNIX YES)
elseif(${TARGET_OS} STREQUAL "macos")
    set(TARGET_OS_MACOS YES)
    set(TARGET_OS_APPLE YES)
    set(TARGET_OS_HOST YES)
    set(TARGET_OS_UNIX YES)
elseif(${TARGET_OS} STREQUAL "linux")
    set(TARGET_OS_LINUX YES)
    set(TARGET_OS_HOST YES)
    set(TARGET_OS_UNIX YES)
elseif(${TARGET_OS} STREQUAL "windows")
    set(TARGET_OS_WINDOWS YES)
    set(TARGET_OS_HOST YES)
elseif(${TARGET_OS} STREQUAL "web")
    set(TARGET_OS_WEB YES)
else()
    message(FATAL_ERROR "Don't know how to deal with CMAKE_SYSTEM_NAME \"${CMAKE_SYSTEM_NAME}\"")
endif()

if(${CMAKE_HOST_SYSTEM_NAME} STREQUAL "Darwin")
    set(HOST_OS "macos")
    set(HOST_OS_MACOS YES)
    set(HOST_OS_UNIX YES)
elseif(${CMAKE_HOST_SYSTEM_NAME} STREQUAL "Linux")
    set(HOST_OS "linux")
    set(HOST_OS_LINUX YES)
    set(HOST_OS_UNIX YES)
elseif(${CMAKE_HOST_SYSTEM_NAME} STREQUAL "Windows")
    set(HOST_OS "windows")
    set(HOST_OS_WINDOWS YES)
else()
    message(FATAL_ERROR "Don't know how to deal with CMAKE_HOST_SYSTEM_NAME \"${CMAKE_HOST_SYSTEM_NAME}\"")
endif()

add_definitions(-DTARGET_OS="${TARGET_OS}")
foreach(FLAG in items ANDROID IOS MACOS LINUX WINDOWS MOBILE HOST APPLE UNIX WEB)
    if(TARGET_OS_${FLAG})
        add_definitions(-DTARGET_OS_${FLAG}=1)
    endif()
endforeach()
